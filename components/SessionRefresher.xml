<?xml version="1.0" encoding="utf-8" ?>

<component name = "SessionRefresher" extends = "Task" >
    <interface>
        <field id = "sess_key" type = "string" value = "" />
    </interface>

    <script type = "text/brightscript" >
        <![CDATA[

        sub init()
            m.top.functionName = "do_refresh"
        end sub

        sub do_refresh()
            exp = createObject("roDateTime")
            exp.mark()
            exp.fromseconds(exp.asseconds() + 365*24*60*60)
  
            sess_cook = {
                "version": 1,
                "domain": m.global.COOKIE_DOMAIN,
                "path": "/",
                "name": "session",
                "value": m.top.sess_key,
                "expires": exp
                }
            cookies = createObject("roArray", 1, false)
            cookies.setentry(0, sess_cook)

            readInternet = createObject("roUrlTransfer")
            readInternet.SetCertificatesFile("common:/certs/ca-bundle.crt")
            readInternet.AddHeader("X-Roku-Reserved-Dev-Id", "")
    
            readInternet.EnableCookies()
            retval = readInternet.AddCookies(cookies)
            if retval <> true
                print "ERROR: Cookie add failed!"
            end if

            readInternet.InitClientCertificates()
            readInternet.setUrl(m.global.URI_REFRESH_SESS)

            msg_port = createObject("roMessagePort")
            readInternet.setMessagePort(msg_port)

            retval = readInternet.AsyncGetToString()
            if retval = false
                print "ERROR: Async get returned false!"
            end if

            msg = wait(5000, msg_port)

            if msg = invalid
                print "ERROR: Response was invalid!"
                m.top.sess_key = "INVALID"
                return
            end if

            if msg.GetResponseCode() = 200 then
                headers = msg.GetResponseHeaders()
                set_cookie = headers["set-cookie"]

                session_loc = set_cookie.instr("session=")
                after_sess = set_cookie.right(set_cookie.len() - 8)
                sess_end = after_sess.instr(";")
                access_token = after_sess.left(sess_end)
                
                reg_sec = CreateObject("roRegistrySection", m.global.REG_SEC)
                reg_sec.write(m.global.REG_SEC_TOKEN_NAME, access_token)
                reg_sec.flush()

                m.top.sess_key = access_token
            else
                print "Refresher INVALID"
                m.top.sess_key = "INVALID"
            end if
        end sub
        ]]>
    </script>

</component>
